real L=1;
int gammaB=1, gammaV=2, gammaAir=3;
//hypoderme
real Hh=0.4;
border bas (t=0, L){x=t; y=0; label =gammaB;}
border droith (t=0, Hh){x=L; y=t; label =gammaV;}
border ligne1 (t=L, 0){x=t; y=Hh; label =3;}
border gaucheh (t=Hh, 0){x=0; y=t; label =gammaV;}

//derme
real Hd=0.24;
//border bas=-ligne1 
border droitd (t=0+Hh, Hd+Hh){x=L; y=t; label =gammaV;}
border ligne2 (t=L, 0){x=t; y=Hd+Hh; label =6;}
border gauched (t=Hd+Hh, 0+Hh){x=0; y=t; label =gammaV;}

//epiderme
real He=0.005;
//border bas=-ligne2 
border droite (t=0+Hh+Hd, He+Hh+Hd){x=L; y=t; label =gammaV;}
border ligne3 (t=L, 0){x=t; y=He+Hh+Hd; label =9;}
border gauchee (t=Hd+Hh+He, 0+Hh+Hd){x=0; y=t; label =gammaV;}

//air
real Ha=0.1;
//border bas=-ligne3
border droita (t=0+Hh+Hd+He, Hh+Hd+He+Ha){x=L; y=t; label =gammaV;}
border haut (t=L, 0){x=t; y=Hd+Hh+He+Ha; label =gammaAir;}
border gauchea (t=Hd+Hh+He+Ha, 0+Hh+Hd+He){x=0; y=t; label =gammaV;}

real meshsize=0.01;
int Nx= L/meshsize;
int Nyh=Hh/meshsize;
int Nyd=Hd/meshsize;
int Nye=5;
int Nya=Ha/meshsize;

func bordh = bas(Nx)+droith(Nyh)+ligne1(Nx)+gaucheh(Nyh);
func bordd = droitd(Nyd)+ligne2(Nx)+gauched(Nyd);
func borde = droite(Nye)+ligne3(Nx)+gauchee(Nye);
func borda = droita(Nya)+haut(Nx)+gauchea(Nya);
plot (bordh+bordd+borde+borda);

mesh Th = buildmesh(bordh+bordd+borde+borda);
plot(Th);

cout << "Hypoderme has region number " << Th(0.1, 0.1).region << endl;
cout << "Derme has region number " << Th(0.1, 0.1+Hh).region << endl;
cout << "Epiderme has region number " << Th(0.1, 0.1+Hh+Hd).region << endl;
cout << "Air has region number " << Th(0.1, 0.1+Hh+Hd+He).region << endl;
int regHypoderme=Th(0.1, 0.1).region;
int regDerme=Th(0.1, 0.1+Hh).region;
int regEpiderme=Th(0.1, 0.1+Hh+Hd).region;
int regAir=Th(0.1, 0.1+Hh+Hd+He).region;


fespace Vh(Th, P1);
Vh T,Tp;
macro grad(u) [dx(u), dy(u)]//
// Constantes physiques
real s=40, gb=36, Text=20, a=10000;

// Tableau et fonction permettant de dÃ©finir les constantes dans chaque domaine
real[int] rhotab = [ 1.2, 1.2, 1.2 ,1.2e-3 ];
func rho = rhotab[0]*(region==regHypoderme) + rhotab[1]*(region==regDerme) + rhotab[2]*(region==regEpiderme) + rhotab[3]*(region==regAir);

real[int] Ktab = [ 21 , 53 , 46 , 2.34 ];
func K = Ktab[0]*(region==regHypoderme) + Ktab[1]*(region==regDerme) + Ktab[2]*(region==regEpiderme) + Ktab[3]*(region==regAir);

real[int] Ctab = [ 3.6, 3.8, 3.8, 1.004 ];
func C = Ctab[0]*(region==regHypoderme) + Ctab[1]*(region==regDerme) + Ctab[2]*(region==regEpiderme) + Ctab[3]*(region==regAir);

solve EqChaleur(T,Tp)=int2d(Th)(K*grad(T)'*grad(Tp))
+int1d(Th,gammaAir)(a*Tp*T)
-int1d(Th,gammaAir)(a*Text*Tp)
-int2d(Th)(s*Tp)
+on(gammaB,T=gb);

plot(T,value=1,fill=1);

//Add time with BDF2 and implicit Newton
//Solve coupled equation
//Make Quadranglangle mesh
//Add terme with u
